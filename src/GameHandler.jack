class GameHandler{
    static int boardSize;

    function void startGame(){
        var String firstNameAsk; var String secondNameAsk;
        var String firstName; var String secondName; 
        var Player firstPlayer;  var Player secondPlayer;
        var Game game;
        var Board board;
        var int input;
        var InputUtil inputUtil;

        let boardSize = 3;
        let inputUtil = InputUtil.new();

        let firstNameAsk = "First player name:";
        let secondNameAsk = "Second player name:";

        let firstName = Keyboard.readLine(firstNameAsk);
        do inputUtil.eraseMessage();
        do firstNameAsk.dispose();

        let secondName = Keyboard.readLine(secondNameAsk);
        do inputUtil.eraseMessage();
        do secondNameAsk.dispose();
        
        let firstPlayer = Player.new(firstName, 1, CellValue.x());
        let secondPlayer = Player.new(secondName, 2, CellValue.y());
        
        let board = Board.new(boardSize);
        let game = Game.new(firstPlayer, secondPlayer, board);

        do inputUtil.eraseMessage();

        do ScreenHelper.drawGameField();

        while (game.getGameState() = GameState.playing()) {
            let input = inputUtil.readInput("Enter number of cell. For reset type 0:");

            if (input = 0) {
                do board.clear();
            }

            do GameHandler.handlePlayerMove(input,game);
        }
        do game.dispose();
        return;
    }


    function void handlePlayerMove(int input, Game game){
        var int x; var int y;
        var Player currentPlayer;
        var boolean succsess; 

        let x = (input - 1) / boardSize;
        let y = MathExt.mod(input - 1, boardSize);

        let currentPlayer = game.getCurrentPlayer();
        
        let succsess = game.tryMakeMove(x,y);
        if (succsess){
            if (currentPlayer.getValue() = CellValue.x()){
                do ScreenHelper.drawCross(x, y);
            }
            else{
                do ScreenHelper.drawCircle(x, y);
            }
            do game.switchPlayer();
        }
        
        
        return;
    }


    
}