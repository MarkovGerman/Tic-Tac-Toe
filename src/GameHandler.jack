class GameHandler{
    static int boardSize;

    function void start(){
        var InputUtil inputUtil;
        var Game game;
        var int input;
        var int size;

        let size = 3;
        let inputUtil = InputUtil.new();

        let game = GameHandler.handleInitNewGame(inputUtil, size);

        while (game.getGameState() = GameState.playing()) {
            let input = inputUtil.readInput("Enter number of cell. For reset type 0:");

            if (input = 0) {
                do Screen.clearScreen();
                let game = GameHandler.handleInitNewGame(inputUtil, size);
            
            }
            else{
                do GameHandler.handlePlayerMove(input, game, size);
            }
        }

        do game.dispose();
        return;
    }

    function Game handleInitNewGame(InputUtil inputUtil, int boardSize){

        var String firstNameAsk; var String secondNameAsk;
        var String firstName; var String secondName; 
        var Player firstPlayer; var Player secondPlayer;
        var Board board;
       
        let firstNameAsk = "First player name:";
        let secondNameAsk = "Second player name:";

        let firstName = Keyboard.readLine(firstNameAsk);
        do inputUtil.eraseMessage();
        do firstNameAsk.dispose();

        let secondName = Keyboard.readLine(secondNameAsk);
        do inputUtil.eraseMessage();
        do secondNameAsk.dispose();
        
        let firstPlayer = Player.new(firstName, 1, CellValue.x());
        let secondPlayer = Player.new(secondName, 2, CellValue.y());
        
        let board = Board.new(boardSize);

        do ScreenHelper.drawGameField();

        return Game.new(firstPlayer, secondPlayer, board);
    }


    function void handlePlayerMove(int input, Game game, int boardSize){
        var int x; var int y;
        var Player currentPlayer;
        var boolean succsess; 

        let x = (input - 1) / boardSize;
        let y = MathExt.mod(input - 1, boardSize);

        let currentPlayer = game.getCurrentPlayer();
        
        let succsess = game.tryMakeMove(x,y);
        if (succsess){
            if (currentPlayer.getValue() = CellValue.x()){
                do ScreenHelper.drawCross(x, y);
            }
            else{
                do ScreenHelper.drawCircle(x, y);
            }
            do game.switchPlayer();
        }
        
        
        return;
    }


    
}